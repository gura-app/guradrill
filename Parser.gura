#!/usr/bin/env gura
import(re)
import(cairo)

GetWidthEm(cr:cairo.context) = {
	cr.text_extents('m').width
}

DrawBorder(cr:cairo.context, x:number, y:number, width:number) = {
	fontExt = cr.font_extents()
	cr.rectangle(x, y - fontExt.ascent * (1 + .2), width, fontExt.ascent * (1 + .2 + .2))
	cr.stroke()
}

DrawText(cr:cairo.context, x:number, y:number, text:string, drawFlag:boolean => true) = {
	textExt = cr.text_extents(text)
	if (drawFlag) {
		cr.move_to(x, y)
		cr.show_text(text)
	}
	[x + textExt.x_advance, y]
}

DrawSymbol(cr:cairo.context, x:number, y:number, text:string, drawFlag:boolean => true) = {
	text = if (text == '*') {'\u00d7'} elsif (text == '/') {'\u00f7'} else {text}
	textExt = cr.text_extents(text)
	if (drawFlag) {
		fontExt = cr.font_extents()
		cr.move_to(x, y - textExt.y_bearing - (fontExt.ascent + textExt.height) / 2)
		cr.show_text(text)
	}
	[x + textExt.x_advance, y]
}

Item = class {
	public {type, text, children, width, widthUnit, align, border}
	__init__(type:symbol, text?:string) = {
		this.type = type	// `Root, `Block, `Text, `Operator
		this.text = text
		this.children = []
		this.width = nil
		this.widthUnit = nil
		this.align = nil
		this.border = false
	}
	AddChild(item:Item) = this.children.add(item)
	

	Print(indentLevel:number => 0):void = {
		if (this.type == `Root) {
			printf('%*s%s:\n', indentLevel * 2, '', this.type)
		} elsif (this.type == `Block) {
			printf('%*s%s: width=%s,align=%s,border=%s\n',
				   indentLevel * 2, '', this.type, this.width, this.align, this.border)
		} else {
			printf('%*s%s: "%s"\n', indentLevel * 2, '', this.type, this.text)
		}
		this.children:*Print(indentLevel + 1)
	}
}

DrawItem(cr:cairo.context, x:number, y:number, item:Parser.Item, drawFlag:boolean => true) = {
	if (item.type == `Text) {
		DrawText(cr, x, y, item.text, drawFlag)
	} elsif (item.type == `Operator) {
		widthEm = GetWidthEm(cr)
		x += widthEm * .4
		[x, y] = DrawSymbol(cr, x, y, item.text, drawFlag)
		x += widthEm * .4
		[x, y]
	} elsif (item.type == `Bracket) {
		widthEm = GetWidthEm(cr)
		[x, y] = DrawSymbol(cr, x, y, '(', drawFlag)
		[x, y] = DrawItems(cr, x, y, item.children, drawFlag)
		x += widthEm * .15
		[x, y] = DrawSymbol(cr, x, y, ')', drawFlag)
		[x, y]
	} elsif (item.type == `Block) {
		width = item.width * GetWidthEm(cr)
		[wdContent, htContent] = ExtentItems(cr, item.children) // htContent is always zero
		DrawItems(cr, x + (width - wdContent) / 2, y, item.children, drawFlag)
		if (item.border && drawFlag) {
			DrawBorder(cr, x, y, width)
		}
		[x + width, y]
	} else {
		DrawItems(cr, x, y, item.children, drawFlag)
	}
}

DrawItems(cr:cairo.context, x:number, y:number, items[]:Parser.Item, drawFlag:boolean => true) = {
	items.each {|item|
		[x, y] = DrawItem(cr, x, y, item, drawFlag)
	}
	[x, y]
}

ExtentItems(cr:cairo.context, items[]:Parser.Item) = {
	[x, y] = DrawItems(cr, 0, 0, items, false)
	[x, 0]
}

Parse(str:string) = {
	itemStack = []
	itemStack.add(Item(`Root))
	text = ''
	stat = `Normal
	str.each {|ch|
		itemParent = itemStack.last()
		if (stat == `Normal) {
			if (ch == '{') {
				item = Item(`Block)
				itemParent.AddChild(item)
				itemStack.add(item)
			} elsif (ch == '}') {
				if (itemParent.type != `Block) {
					raise(error.SyntacError, 'unbalanced bracket')
				}
				if (!text.isempty()) {
					itemParent.AddChild(Item(`Text, text))
					text = ''
				}
				stat = `BlockAttrPre
			} elsif (ch == '(') {
				item = Item(`Bracket)
				itemParent.AddChild(item)
				itemStack.add(item)
			} elsif (ch == ')') {
				if (itemParent.type != `Bracket) {
					raise(error.SyntacError, 'unbalanced bracket')
				}
				if (!text.isempty()) {
					itemParent.AddChild(Item(`Text, text))
					text = ''
				}
				itemStack.erase(-1)
				text = ''
			} elsif (ch in ['+', '-', '*', '/', '^', '=', '<', '>']) {
				if (!text.isempty()) {
					itemParent.AddChild(Item(`Text, text))
					text = ''
				}
				itemParent.AddChild(Item(`Operator, ch))
			} elsif (ch in [' ', '\t']) {
				if (!text.isempty()) {
					itemParent.AddChild(Item(`Text, text))
					text = ''
				}
			} else {
				text += ch
			}
		} elsif (stat == `BlockAttrPre) {
			if (ch == '[') {
				stat = `BlockAttr
			} else {
				itemStack.erase(-1)
				text += ch
				stat = `Normal
			}
		} elsif (stat == `BlockAttr) {
			if (ch == ']') {
				text.split(',') {|field|
					field = field.strip()
					if (m = field.match(r'([\d\.]+)([a-z]+)')) {
						itemParent.width = m[1].tonumber()
						itemParent.widthUnit = m[2]
					} elsif (m = field.match(r'border=([a-z]+)')) {
						if (m[1] == 'thin') {
							itemParent.border = `thin
						} elsif (m[1] == 'medium') {
							itemParent.border = `medium
						} elsif (m[1] == 'thick') {
							itemParent.border = `thick
						}
					} elsif (field == 'border') {
						itemParent.border = `medium
					} elsif (field == 'center') {
						itemParent.align = `center
					} elsif (field == 'left') {
						itemParent.align = `left
					} elsif (field == 'right') {
						itemParent.align = `right
					} else {
						// invalid attribute
					}
				}
				itemStack.erase(-1)
				text = ''
				stat = `Normal
			} else {
				text += ch
			}
		}
	}
	!text.isempty() && itemParent.AddChild(Item(`Text, text))
	itemStack.first()
}

if (__name__ == '__main__') {
	//str = 'x^2+3x^3+4=0'
	str = '{1000}[5em,center] + {123}[5em,center] = {}[5em,border=thin]'
	//str = '123+345-1111'
	
	Parse(str).Print()
}
